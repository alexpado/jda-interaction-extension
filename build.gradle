import java.nio.charset.StandardCharsets

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.41.0'
    id 'org.ajoberstar.reckon' version '0.13.1'
}

def generalCharset = StandardCharsets.UTF_8.name();

reckon {
    scopeFromProp()
    stageFromProp('dev', 'beta', 'rc', 'final')
}

group 'fr.alexpado'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

compileJava.options.encoding = generalCharset
compileTestJava.options.encoding = generalCharset
javadoc.options.encoding = generalCharset

repositories {
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    mavenLocal()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-alpha.4'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'JDA Interaction Tools - JDA interaction made easier'

                url = 'https://github.com/alexpado/jda-interaction-extension'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'alexpado'
                        name = 'Alexandre PADOVAN'
                        email = 'contact@alexpado.fr'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/alexpado/jda-interaction-extension.git'
                    developerConnection = 'scm:git:ssh://github.com/alexpado/jda-interaction-extension.git'
                    url = 'https://github.com/alexpado/jda-interaction-extension/tree/main'
                }
            }
        }
    }
}
